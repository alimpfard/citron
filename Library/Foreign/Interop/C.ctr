import Library/Data/Map.

#:language XFrozen

Inject on: 'importFunctionsOrTypes:fromHeaders:andLibraries:' do: {:funcs:headers:lib
  var fns is getFunctionsOrTypes: funcs fromHeaders: headers andLibraries: lib.
  fns each: {:key:value
    Reflect objectExists: key, ifTrue: {
      thisBlock error: 'Object $$key exists, I refuse to replace it'.
    }.
    Reflect addGlobalVariable: key.
    Reflect set: key to: value.
  }.
}.

Inject on: 'importFunctionsOrTypes:' do: {:funcs
  var fns is getFunctionsOrTypes: funcs.
  fns each: {:key:value
    Reflect objectExists: key, ifTrue: {
      thisBlock error: 'Object $$key exists, I refuse to replace it'.
    }.
    Reflect addGlobalVariable: key.
    Reflect set: key to: value.
  }.
}.

Inject on: 'getFunctionsOrTypes:fromHeaders:andLibraries:' do: {:self:funcs:headers:libs
  var res is (var ctx is newWithHeaders: headers andLibraries: libs) getFunctionsOrTypes: funcs.
  # ctx destruct.
  ^res.
}.

Inject on: 'normalizeHeaderString:' do: {:str
  str startsWith: '"', ifTrue: {
    ^str.
  } ifFalse: {
    str startsWith: '<', ifTrue: {
      ^str.
    } ifFalse: {
      ^'<${{str}}$>'.
    }.
  }.
}.

Inject on: 'newWithHeaders:andLibraries:' do: {:self:headers:lib
  var context is me new.
  me includePaths each_v: {:path context addIncludePath: path. }.
  me libraryPaths each_v: {:path context addLibraryPath: path. }.
  lib isNil ifFalse: {
    lib each_v: {:lib
      context linkInLibrary: lib.
    }.
  }.
  context compile: ((['#include %s\n' % [self normalizeHeaderString: x],, (x: headers)]) join: '').
  ^context.
}.

CIF on: 'toString' do: {
  ^'<${{me isFunctionPointer either: 'Function pointer' or: 'Function'}}$ with ${{me argumentCount}}$ argument(s)>'.
}.
# CTypes on: 'toString' do: {:self
#   ^'<CTypes type_id: ${{Reflect getProperty: ':cType' ofObject: me}}$>'.
# }.

Inject on: 'getFunctionsOrTypes:' do: {:self:funcs
  var &context is self.
  funcs isA: Array, ifTrue: {
    var fnvs is Map new.
    funcs each_v: {:v fnvs put: v at: v. }.
    funcs is fnvs.
  }.
  #:declare lazyev if-then-else
  var if-then-else is \:cond:ift:iff cond evaluate ifTrue: { ^ift evaluate. } ifFalse: { ^iff evaluate. }.
  var cifs is context definedFunctions: funcs.
  cifs each: {:k:v
    # Pen writeln: '${{k}}$ -> ${{v}}$'.
  }.
  funcs isNil ifTrue: {
    funcs is cifs keys.
    var fnvs is Map new.
    funcs each_v: {:v fnvs put: v at: v. }.
    funcs is fnvs.
  }.
  var str is 'void dummy' + (context id toString) + '() {return %:L;}' % [
    '+\n',
    ['((int)&%s)' % [xs],, (xs: (funcs keys filter_v: \:fn cifs @ fn isA: CIF))]
  ].
  # Pen writeln: str.
  context compile: str.
  ^(funcs kvmap:
    {:kv
      var k is kv head.
      var v is kv last.
      # Pen writeln: '%40s = %40s %40s' % [k, v, context symbol: k].
      ^[v,
       if-then-else {
        (cifs at: k, isA: CIF),
        (context
          generateCFunctionWrapperFor: (context symbol: k)
          withCIF: (cifs at: k)
          andName: v),
        (cifs at: k)
      }
    ].
  }).
}.

Inject on: 'generateCFunctionWrapperFor:withCIF:andName:' do: {:sym:cif:x
  sym get = 0 ifTrue: {
    thisBlock error: 'Function $$x has not been defined, or does not exist'.
  }.
  var genName is {:i
    var name is $(ref) withValue: 'ident$$i'.
    name type: 'REFERENCE'.
    ^name.
  }.
  # TODO add a way to deep-copy ASTs
  var av0 is AST parse: '{:catch ^my cif call: my symbol run withArgs: args. }.', at: 0.
  var args is av0 at: 0.
  var argvs is (var argfs is AST parse: '[].', at: 0) at: 0.
  cif argumentCount times: {:i
    argvs insert: (var name is genName[i]) at: -1.
    args insert: name at: 0.
  }.
#    instrs return  exprm  kwmesg  'args'
  av0 at: 1, at: 0, at: 0, at: 1, put: argfs at: 1.
  # Pen writeln: ast2ctrcode[av0].
  var pointer is CTypes pointer.
  ^($!av0)
    on: 'cPtr' do: { ^my symbol run. },
    set: 'cif' value: cif,
    set: 'symbol' value: (
      cif isFunctionPointer not
        either: {^{\ sym}.}
        or: {^{\
          sym derefAs: pointer.
        }.}
    ).
}.

Inject on: 'getDefines:fromHeaders:' do: {:self:defs:headers
  var context is me new.
  me includePaths each_v: {:path context addIncludePath: path. }.
  var sheader is ((['#include %s\n' % [self normalizeHeaderString: x],, (x: headers)]) join: '').
  sheader +=: '#define Cprocess_numbers 0\n'.
  sheader +=: '#define CTR_INJECT_CAST_STR(x) CTR_INJECT_CAST_ST_STR(x)\n'.
  sheader +=: '#define CTR_INJECT_CAST_ST_STR(x) #x\n'.
  sheader +=: 'typedef struct { void* ptr; void* next; } ctr_argument;\n'.
  sheader +=: 'extern void* ctr_array_push(void*,void*);\n'.
  sheader +=: 'extern void* ctr_build_string_from_cstring(char*);\n'.
  sheader +=: 'extern void* ctr_build_number_from_float(double);\n'.
  sheader +=: 'extern void* get_CtrStdArray();\n'.
  var ptr is CTypes pointer.
  var sbody is defs
    foldl: (\:&acc:&def '${{acc}}$
      (CTR_INJECT_CAST_STR(${{def}}$) "\\n");
      ptr = ctr_array_push(ptr, &(ctr_argument){
#if Cprocess_numbers && isnumeric(${{def}}$)
        #warning Will now produce numbers for ${{def}}$
        ctr_build_number_from_float(${{def}}$)
#else
        ctr_build_string_from_cstring(CTR_INJECT_CAST_STR(${{def}}$))
#endif
      });')
    accumulator: ''.
  var body is '$$sheader void* main(void* ptr) {
    $$sbody
    return ptr;
  }'.
  # Pen writeln: body.
  var main is context compile: body, symbol: 'main'.
  var values is Array new.
  CIF newByInferringTypes: [ptr, ptr], call: main withArgs: [ptr addressOfObject: values].
  var ret is Map new.
  defs each: {:i:x ret put: (values at: i) at: x. }.
  context destruct.
  ^ret.
}.

Inject on: 'importDefines:fromHeaders:' do: {:defs:headers
  defs is getDefines: defs fromHeaders: headers.
  defs each: {:key:value
    Reflect objectExists: key, ifTrue: {
      thisBlock error: 'Object $$key exists, I refuse to replace it'.
    }.
    Reflect addGlobalVariable: key.
    Reflect set: key to: ({^value toNumber.} catch: {^value.}, run).
  }.
}.

Inject on: 'struct' do: {:self
  ^Object cnew: {
    inject => self.
    on: 'respondTo:' do: {:name
      var inject is my inject.
      ^Object cnew: {
        inject => const inject.
        name => name.
        on: 'applyAll:' do: {:&descriptor
          # Type msg -> normal field
          # Type msg: num -> bitfield
          # limitation: no inner struct
          var strdsc is ''.
          descriptor count times: {:i
            var dsc is descriptor at: i.
            var typename is dsc at: 0, value.
            var rest is dsc at: 1.
            rest type = 'KWMESSAGE' ifTrue: {
              # bitfield
              &&rest is '${{rest at: 0, value}}$ ${{rest at: 1, value}}$'.
            } ifFalse: {
              &&rest is rest value.
            }.
            dsc count > 2 ifTrue: {
              # some array
              dsc is dsc at: 2.
              (dsc type = 'KWMESSAGE') & (dsc value = 'applyAll:') ifFalse: {
                thisBlock error: 'Expected `type ident [ length ]\' in struct specifier'.
              }.
              rest +=: '[${{dsc at: 0, at: 0, at: 0, value}}$]'.
            }.
            strdsc +=: '$$typename ${{rest}}$; '.
          }.
          strdsc is 'struct ${{my name}}$ { $$strdsc };'.
          # Pen writeln: strdsc.
          my inject compile: strdsc, importFunctionsOrTypes: [my name].
          my inject destruct.
          ^ Nil.
        }.
      }.
    }.
  }.
}.

var C is Object cnew: {
    on: 'process:' do: {:st
        var ff is Inject compile: st.
        ^Object cnew: {
            ff => ff.
            on: 'process:' do: {:sym
                my ff importFunctionsOrTypes: [sym].
            }.
            learn: 'respondTo:' means: 'process:'.
        }.
    }.
}.
