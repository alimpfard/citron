import genAccessors: 'return'.
import return: (Map new).

import genAccessors: '__is_being_imported'.
import __is_being_imported: False.

import genAccessors: '__loaded_modules'.
import __loaded_modules: Map new.

import on: 'searchPaths' do: {^my searchPaths.}.
import on: 'export:as:' do: {:object:name
  Reflect addGlobalVariable: name.
  Reflect set: name to: object.
}.
import on: 'require-parent' do: {
  me message: '..' arguments: [].
}.
import on: 'dir:' do: {:dname
  me message: dname arguments: [].
}.
import on: 'return:' do: {:obj
  var path is (File new: '.', realPath).
  var spath is my searchPaths filter: {:i:v ^path contains: v.}.
  var import_name is path skip: (spath at: 0, length + 1).
  my return put: obj at: import_name.
}.
import on: 'return' do: {}.
import on: '__realloc:' do: {:map
  var m is Map new.
  map each: {:i:v m put: v at: i.}.
  ^m.
}.
import on: '__load:' do: {:name
  var file is Nil.
  var path is Nil.
  var is_dir is False.
  my searchPaths each: {:i:v
  # Pen write: v, brk.
    file isNil not break.
    (var f is File new: (v + '/$$name' + '.ctr')) exists ifTrue: {
      (File typeOf: (f realPath), = 'file') ifTrue: {
        path is File new: v, realPath.
        file is f.
      }.
    }.
    (var f is File new: (v + '/$$name')) exists ifTrue: {
      (File typeOf: (v + '/$$name'), = 'folder') ifTrue: {
        path is f realPath.
        file is File new: (path + '/main.ctr').
        is_dir is True.
      }.
    }.
  }.
  file isNil ifTrue: {
    thisBlock error: ('Could not find neither a package, nor a standalone file representing $$name in the search paths: \n%s' % [(my searchPaths fmap: {:p ^p + ' aka (' + (File new: p, realPath) + ')'.}, join: '\n')]).
  }.
  var _lm is my __loaded_modules.
  ^(_lm contains: path)
    either: {
      Program warning: '[ctr]::module $$path was already loaded'.
      ^_lm at: path.
    }
    or: {
      var oldp is Program changeDirectory: path.
      var imp is import __is_being_imported.
      import __is_being_imported: True.
      var pf is Reflect runInNewContext: { file runHere. }.
      import __is_being_imported: imp.
      Program changeDirectory: oldp.
      _lm put: pf at: path.
      ^pf.
    }.
}.
import on: 'respondTo:' do: {:name me __load: name. }.
import on: 'respondTo:and:' do: {:mods:*names
  var loaded is mods split: ':', fmap: \:mod import __load: mod.
  ^names imap: {:i:names
    var mod is import __realloc: loaded @ i.
    ^Reflect isObject: names linkedTo: Array,
      either: {
        ^names fmap: {:name var p is mod at: name. import export: p as: name. ^p.}.
      }
      or: {
        var p is mod at: names.
        import export: p as: names.
        ^p.
      }.
  }, foldl: (\:acc:x acc + (Reflect isObject: x linkedTo: Array, either: x or: [x])) accumulator: Array new.
}.
['respondTo:and:and:', 'respondTo:and:and:and:', 'respondTo:and:and:and:and:', 'respondTo:and:and:and:and:and:'] fmap: \:msg import learn: msg means: 'respondTo:and:'.
